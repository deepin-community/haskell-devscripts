#!/usr/bin/perl

# Copyright Â© 2022 Felix Lechner <felix.lechner@lease-up.com>
#
# based on a shell script by the same name
#     Joachim Breitner <nomeata@debian.org>
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

use v5.20;
use warnings;
use utf8;

use Const::Fast;
use Path::Tiny;

use Debian::Debhelper::Buildsystem::Haskell::Recipes qw(
  run
  installable_hc
);
use Debian::Debhelper::Dh_Lib;

const my $EMPTY => q{};
const my $NEWLINE => qq{\n};

init();

for my $installable (@{ $dh{DOPACKAGES} }) {

    my $compiler = installable_hc($installable) || $ENV{DEB_DEFAULT_COMPILER};

    unless (length $compiler) {

        warning("No Haskell compiler for installable $installable.");
        next;
    }

    my $version= run(qw{dpkg-query --showformat=${Version} --show}, $compiler);
    chomp $version;

    unless (length $version) {

        warning("No version for Haskell compiler $compiler");
        next;
    }

    # strip Debian revision
    $version =~ s{ - [^-]+ $}{}x;

    my $substvars_path = "debian/$installable.substvars";

    replace_line($substvars_path, "haskell:$compiler-version",
        "$compiler (>= $version), $compiler (<< $version+)");
}

exit;

sub replace_line {
    my ($path, $field, $value) = @_;

    $value //= $EMPTY;

    path($path)->touch;

    my @lines = grep { !m{^ $field = }x } path($path)->lines_utf8;

    push(@lines, "$field=$value" . $NEWLINE);

    path($path)->spew_utf8(@lines);

    return;
}

=head1 NAME

dh_haskell_compiler - store Haskell compiler info in substvars

=head1 SYNOPSIS

B<dh_haskell_compiler> [S<I<debhelper options>>]

=head1 DESCRIPTION

dh_haskell_compiler is a Debhelper program that stores information about Haskell compilers in substvars.

You can use it like this: C<${haskell:ghc-version}>

=head1 SEE ALSO

L<debhelper(7)>

=head1 AUTHOR

Joachim Breitner <nomeata@debian.org>

=cut

# Local Variables:
# indent-tabs-mode: nil
# cperl-indent-level: 4
# End:
# vim: syntax=perl sw=4 sts=4 sr et
